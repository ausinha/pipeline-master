#!/bin/tcsh
#$ -S /bin/tcsh

##
## USAGE: scripts-qsub-wrapper N-THREADS SCRIPT-PATH OUTPUT-DIR ARGS ...
##

if ($#argv < 3) then
  grep '^##' $0
  exit
endif

set nthreads = $1
set prog = `which $2`
set out = $3
shift
shift

if (! -e $out) then
  set uptodate = false
else
#  find $out -type f -printf '%T+ %p\n' | sort | head -n 1               # need to compare date OUTPUT-DIR was created to the most recent date in SCRIPT-PATH and ARGS
  set uptodate = true
endif

if ($uptodate == false) then
  scripts-send2err "[scripts-qsub-wrapper] Generating results in directory \'$out\'..."
  if (-e $out) rm -rf $out
  scripts-create-path $out/ 
  set pref = $out/job
  scripts-print-cmdline $prog $argv:q >! $pref.sh
  qsub -cwd -o :$pref.out -e :$pref.err -pe threaded $nthreads ./$pref.sh >! $pref.id          # TODO: qsub does not allow ',' in $pref
  cat $pref.id | cut -d' ' -f3                                                                 # TODO: need a general method to obtain job id
else 
  scripts-send2err "[scripts-qsub-wrapper] Directory \'$out\' is up-to-date, skipping..."
endif







