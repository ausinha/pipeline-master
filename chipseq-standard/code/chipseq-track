#!/bin/tcsh

##
## USAGE: chipseq-track OUTPUT-FILE PARAMETER-SCRIPT BAM-FILE
##

if ($#argv != 3) then
  grep '^##' $0
  exit
endif

set out = $1
set params = $2
set aln = $3

# set parameters
source $params
if (! $?NSLOTS) then
  set threads = 8
else
  set threads = $NSLOTS
endif

# sort bam
set sorted_bam = `scripts-create-temp`
samtools sort -@ $threads -m 4G $aln -f $sorted_bam

# create bedgraph
set chr_sizes = `scripts-create-temp`
set bedgraph = `scripts-create-temp`
cat inputs/release/genome.bed | awk '{print $1,$2,$3,$1}' | genomic_regions n >! $chr_sizes
genomeCoverageBed -ibam $sorted_bam -bg -g $chr_sizes >! $bedgraph

# convert to bigwig
bedGraphToBigWig $bedgraph $chr_sizes $out

# cleanup
rm -f $sorted_bam $chr_sizes $bedgraph

